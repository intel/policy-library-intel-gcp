import "tfplan/v2" as tfplan
import "policy_summary"

param valid_actions default [
	["no-op"],
	["create"],
	["update"],
]

doc = {
	"allowed":   ["Intel Cascade Lake", "Intel Ice Lake", "Intel Sapphire Rapids"],
	"category":  "Infrastructure (IaaS)",
	"error":     " is not an allowed min_cpu_platform type.",
	"file_name": "intel-google-compute-instance-enforce-min-cpu-platform.sentinel",
	"name":      "virtualmachine01",
	"md_url":    "https://github.com/intel/terraform-intel-gcp-vm/blob/main/policies.md",
	"parameter": "min_cpu_platform",
	"provider":  "hashicorp/google",
	"resource":  "google_compute_instance",
	"violation": "The configured server type should use an Intel Xeon 3rd or 4th Generation Scalable processor (code-named Ice Lake or Sapphire Rapids)",
}

// Filter resources by type
all_resources = filter tfplan.resource_changes as _, rc {
	rc.type is doc.resource and
		rc.mode is "managed" and
		rc.change.actions in valid_actions
}

// Filter resources that violate a given condition
violators = filter all_resources as _, r {
	r.change.after.min_cpu_platform not in doc.allowed
}

// Build a summary report
summaryreport = {
	"name": doc.name,
	"violations": map violators as _, violation {
		{
			"name":    violation.name,
			"address": violation.address,
			"type":    violation.type,
			"message": violation.change.after.min_cpu_platform + doc.error,
		}
	},
}

main = rule {
	policy_summary.summary(summaryreport, doc)
}
