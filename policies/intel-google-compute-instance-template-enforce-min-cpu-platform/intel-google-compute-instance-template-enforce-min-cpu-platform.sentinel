#Generate mock data
import "tfplan/v2" as tfplan

param valid_actions default [
	["no-op"],
	["create"],
	["update"],
]

summary = func(input) {
	result = false
	if input.violations is empty {
		return true
	} else {
		log(input)
	}
	return result
}

log = func(input) {
	header(input)
	violations(input)
	return null
}

header = func(input) {
	print(
		"\t========================================================================\n",
		"\t                    _       _       _ \n",
		"\t                   (_)     | |     | |\n",
		"\t                    _ _ __ | |_ ___| |\n",
		"\t                   | | '_ \\| __/ _ \\ |\n",
		"\t                   | | | | | ||  __/ |\n",
		"\t                   |_|_| |_|\\__\\___|_|\n",
		"\t\n",
		"\t========================================================================\n",
		"\tName        :" + "intel-google-compute-instance-template-enforce-min-cpu-platform.sentinel\n",
		"\tCategory    :" + "Infrastructure (IaaS)\n",
		"\tProvider    :" + "hashicorp/google\n",
		"\tResource    :" + "google_compute_instance\n",
		"\tParameter   :" + "min_cpu_platform\n",
		"\tCheck       :" + "min_cpu_platform contains\n",
		"\t\n",
		"\tMin CPU Platforms:\n",
		"\t             Intel Cascade Lake\n",
		"\t             Intel Ice Lake\n",
		"\t             Intel Sapphire Rapids\n",
	)
	return null
}

violations = func(input) {
	print(
		"\t========================================================================\n",
		"\tRESOURCE VIOLATIONS\n",
		"\tThe configured server type should use an Intel Xeon 3rd or 4th Generation Scalable processor (code-named Ice Lake or Sapphire Rapids)\n",
		"\t========================================================================\t",
	)
	for input.violations as violation {
		print(
			"\t",
			"name       :" + violation.name + "\n\t",
			"type       :" + violation.type + "\n\t",
			"address    :" + violation.address + "\n\t",
			"message    :" + violation.message + "\t\n",
			"\t------------------------------------------------------------------------\t",
		)
	}
	if (input.violations is not empty) {
		print(
			"\t",
			"Resources out of compliance: " + string(length(input.violations)) + "\n",
			"\t------------------------------------------------------------------------\t",
		)
	}
	return null
}

// Allowed cpu platforms types
allowed_cpu_types = ["Intel Cascade Lake", "Intel Ice Lake", "Intel Sapphire Rapids"]

doc = {
	"id":       "virtualmachine01",
	"resource": "google_compute_instance_template",
	"name":     "Minimum CPU platform must be one of the approved types",
}

// Filter resources by type
all_resources = filter tfplan.resource_changes as _, rc {
	rc.type is doc.resource and
		rc.mode is "managed" and
		rc.change.actions in valid_actions
}

// Filter resources that violate a given condition
violators = filter all_resources as _, r {
	r.change.after.min_cpu_platform not in allowed_cpu_types
}

// Build a summary report
summaryreport = {
	"id":   doc.id,
	"name": doc.name,
	"violations": map violators as _, violation {
		{
			"name":    violation.name,
			"address": violation.address,
			"type":    violation.type,
			"message": violation.change.after.min_cpu_platform + " is not an allowed min_cpu_platform type.",
		}
	},
}

main = rule {
	summary(summaryreport)
}
